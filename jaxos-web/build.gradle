apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'application'

mainClassName = 'com.github.basking2.jaxos.JaxosWebMain'
applicationDefaultJvmArgs = ["-Dport=3000"]

// group = defined by parent.
// archiveBaseName = defined by parent and project name.
// version = defined by parent.

compileJava {
	options.compilerArgs << '-Xlint:unchecked'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

repositories {
}

dependencies {
    compile project(':jaxos-core')
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:jul-to-slf4j:1.7.2'
    compile 'org.glassfish.grizzly:grizzly-framework:2.3.25'
    compile 'org.glassfish.grizzly:grizzly-http-server:2.3.25'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version:'1.7.21'
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'jaxos-web'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Simple datastructures and algorithms.'
        url 'https://github.com/basking2/jaxos'

        scm {
          connection 'scm:git:git@github.com:basking2/jaxos.git'
          developerConnection 'git@github.com:basking2/jaxos.git'
          url 'https://github.com/basking2/jaxos.git'
        }

        licenses {
          license {
            name 'MIT'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'basking2'
            name 'Sam'
            email 'basking2@yahoo.com'
          }
        }
      }
    }
  }
}
